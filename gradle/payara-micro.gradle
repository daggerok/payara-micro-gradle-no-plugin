import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    ext {
        payaraMicroVersion = "5.194"
    }
}

static String command(Project project) {
    def payaraMicroJar = project.configurations.payaraMicro.asPath
    "java -jar ${payaraMicroJar} --autoBindHttp --clusterName app --deploy ${project.war.archivePath}"
}

static String outputUberJar(Project project) {
    "${project.buildDir}/${project.name}-${project.version}-microbundle.jar"
}

static String[] runInShell(String... command) {
    String[] shell = Os.isFamily(Os.FAMILY_WINDOWS) ? ["cmd", "/c"] : ["sh", "-c"]
    shell + command
}

apply(plugin: "war")
war { // call war file as ROOT.war... it's just a workaround for context-root: "/"
    archiveFileName.set("ROOT.war")
}

repositories { mavenCentral() }
configurations { payaraMicro }
// 5.183 is broken, uber jar is fixed with 5.184
dependencies { payaraMicro("fish.payara.extras:payara-micro:${project.payaraMicroVersion}") }

task bundle(type: Exec) {
    description("build payara-micro application uber jar.")
    shouldRunAfter("clean", "war")
    dependsOn("war")
    commandLine(runInShell("${command(project)} --outputUberJar ${outputUberJar(project)}"))
}

task start(type: Exec) {
    description("run payara-micro application.")
    shouldRunAfter("clean", "war", "bundle")
    dependsOn("bundle")
    commandLine(runInShell("java -jar ${outputUberJar(project)}"))
}

assemble.dependsOn(bundle)
